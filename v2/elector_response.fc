() process_elector_response (int flags, slice in_msg, int msg_value) impure {
  if( flags & 1) {
    ;; Should we process bounced (that is errors) responses somehow?
    ;; Probably no, there is no throws in stake requests except when msg_value < 1
    return (); 
  }
  int incoming_op = in_msg~load_uint(32);
  int query_id = in_msg~load_uint(64);
  (slice _1, slice _2,
   int _3, int _4, 
   int _5, int _6, 
   int _7, int _8, int _9, int stake_on_elector, slice on_fly_query) = load_data();
  (int stored_op, int time, int stored_query_id, slice s_addr) = deserialize_query(on_fly_query);
  ;; After simplification of contract we allow only one request on fly at a time
  ;; in this case we do not afraid to mix query_ids and can use response_query_id as query_id to elector
  int response_query_id = stored_query_id;
  throw_unless(201, stored_query_id == query_id);
  builder qry = empty_query();
  int receipt_attachment = 10000000;

  if ( stored_op == 0x47657424) {
    ;; Refund stake processing
    ;; If stake returns  correctly op = 0xf96f7324, else 0xfffffffe
    ;; https://github.com/newton-blockchain/ton/blob/master/crypto/smartont/elector-code.fc#L403-426
    if(incoming_op == 0xf96f7324) {
      ;; stake_on_elector will be nullified inside
      update_reward_on_stake_return(_1, _2, _3, _4, _5, _6, _7, _8, _9, stake_on_elector, qry, msg_value);
      ;; notify sender about success
      return send_receipt_message(s_addr, stored_op + 0x10000000, response_query_id, 0, receipt_attachment, 1);
    }
    save_data(_1, _2, _3, _4, _5, _6, _7, _8, _9, stake_on_elector, qry);
    if(incoming_op == 0xfffffffe) {
      return send_receipt_message(s_addr, stored_op + 0x20000000, response_query_id, 0, receipt_attachment, 1);
    }
    return send_receipt_message(s_addr, stored_op + 0x0000f000, response_query_id, incoming_op, receipt_attachment, 1);
  }

  if ( stored_op == 0x4e73744b) {
    ;; New stake processing
    ;; Possible answers 0xf374484c (on success) and 0xee6f454c (on error)
    ;; https://github.com/newton-blockchain/ton/blob/master/crypto/smartcont/elector-code.fc#L173
    if(incoming_op == 0xf374484c) {
      save_data(_1, _2, _3, _4, _5, _6, _7, _8, _9, stake_on_elector, qry);
      return send_receipt_message(s_addr, stored_op + 0x10000000, response_query_id, 0, receipt_attachment, 1);
    }
    stake_on_elector = max(0, stake_on_elector - msg_value); ;;What about fee?
    save_data(_1, _2, _3, _4, _5, _6, _7, _8, _9, stake_on_elector, qry);
    if(incoming_op == 0xee6f454c) {
      int error_code = in_msg~load_uint(32);
      return send_receipt_message(s_addr, stored_op + 0x20000000, response_query_id, error_code, receipt_attachment, 1);
    }
    return send_receipt_message(s_addr, stored_op + 0x0000f000, response_query_id, incoming_op, receipt_attachment, 1);
  }
  
  return ();
}
