;; TLB Storage scheme
;; Note address bits in pseudoaddress is sum of address and workchain,
;; chances of collision and overflow are negligible. -1:00..000 has no ability to
;; stake in pool
;; nominator_data#_ staked_amount:Grams locked_amount:Grams = NominatorData;
;;
;; query_id#_ id:uint64 = QueryId;
;; recover_stake_request#47657424 time:uint32 response_query_id:uint64 s_addr:MsgAddressInt = RequestBody;
;; new_stake_request#4e73744b time:uint32 response_query_id:uint64 s_addr:MsgAddressInt amount:Grams = RequestBody;
;; requests#_ dict:(HashmapE QueryId ^RequestBody) = Requests;
;;
;; timestamp#_ time:uint32: Timestamp;
;; transfers#_ recent_outgoing:(HashmapE Timestamp Grams) = RecentTransfers;
;;
;; participant_data#_ address:MsgAddressInt balance:Grams requested_amount:Grams = Participant;
;; empty_query#0 = Query;
;; query#1 op:uint32 time:uint32 response_id:uint64 s_addr:MsgAddressInt = Query;
;; storage#_ 
;;           validator:^Participant
;;           nominator:^Participant
;;           validator_reward_share:uint16
;;           validator_cover_ability:uint16
;;           last_stake_sent:uint32
;;           stake_on_elector:Grams
;;           on_fly_query: Query;

(slice, slice, int, int, int, int, int, int, int, int, slice) load_data () inline {
  var ds = get_data().begin_parse();
  slice v_participant = ds~load_ref().begin_parse();
  slice n_participant = ds~load_ref().begin_parse();
  return (
          v_participant~load_msg_addr(),
          n_participant~load_msg_addr(),
          v_participant~load_grams(),
          n_participant~load_grams(),
          v_participant~load_grams(),
          n_participant~load_grams(),
          ds~load_uint(16),
          ds~load_uint(16),
          ds~load_uint(32),
          ds~load_grams(),
          ds
          );
}

() save_data (
              slice validator,
              slice nominator,
              int validator_balance,
              int nominator_balance,
              int validator_requested_withdrawal,
              int nominator_requested_withdrawal,
              int validator_reward_share,
              int validator_cover_ability,
              int last_stake_sent,
              int stake_on_elector,
              builder on_fly_query) impure inline {
  cell v_participant = begin_cell().store_slice(validator)
                                   .store_grams(validator_balance)
                                   .store_grams(validator_requested_withdrawal)
                       .end_cell();
  cell n_participant = begin_cell().store_slice(nominator)
                                   .store_grams(nominator_balance)
                                   .store_grams(nominator_requested_withdrawal)
                       .end_cell();
  var st = begin_cell().store_ref(v_participant)
                       .store_ref(n_participant)
                       .store_uint(validator_reward_share, 16)
                       .store_uint(validator_cover_ability, 16)
                       .store_uint(last_stake_sent, 32)
                       .store_grams(stake_on_elector)
                       .store_builder(on_fly_query)
            .end_cell();
  set_data(st);
}

(int, int, int, slice) deserialize_query (slice query) {
  if(query~load_int(1)) {
    return (query~load_uint(32), query~load_uint(32), query~load_uint(64), query);
  } else {
    return (0, 0, 0, null());
  }
}

builder empty_query() inline {
  return begin_cell().store_int(0, 1);
}
builder serialize_query(int op, int time, int query_id, slice address) inline {
   return begin_cell().store_int(-1, 1).store_uint(op, 32).store_uint(time, 32)
                      .store_uint(query_id, 64).store_slice(address);
}
