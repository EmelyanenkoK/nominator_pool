
() notify_nominator (slice addr, int pnl, int total, int value) impure {
    return send_message( addr, value,
                  begin_cell().store_uint(op::nominator::notify(), 32).store_int(pnl, 80).store_int(total,80).end_cell(), 1);
}

((int, int), ()) ~consider_withdrawal_requests((int, int) requests, slice validator, slice nominator, int val_balance, int nom_balance) {
    (int val_request, int nom_request) = requests;
    int me = get_balance().pair_first() - fees::storage_reserve(); ;; keep 10 coins for storage
    val_request = min(val_request, val_balance);
    nom_request = min(nom_request, nom_balance);
    if((nom_request > fees::withdraw()) & (nom_request <= me)) {
      send_message(nominator, nom_request - fees::withdraw(), begin_cell().store_uint(op::validator::withdrawal(), 32).end_cell(), 0);
      me -= nom_request;
      nom_request = 0;
    }
    if( (~ (nom_request > fees::withdraw())) & (val_request > fees::withdraw()) & (val_request <= me)) {
      send_message(validator, val_request - fees::withdraw(), begin_cell().store_uint(op::validator::withdrawal(), 32).end_cell(), 0);
      val_request = 0;
    }
    return ((val_request, nom_request), ());
}

((int, int, int, int), ()) ~update_balances_on_stake_return( (int, int, int, int) data,
                                 slice validator, slice nominator,
                                 int validator_reward_share,
                                 int matured,
                                 int  msg_value) impure {
    (int val_balance, int nom_balance,
     int val_request, int nom_request) = data;
    int profit  = msg_value - matured - fees::profit_notification() - fees::profit_operation();
    int nominator_profit = 0;
    ;; Note validator profit is consist of two parts:
    ;;  1) validation part proportional to validator_reward_share
    ;;  2) nomination part proportional to his stake
    if( profit >= 0 ) {
      int validation_profit = profit * validator_reward_share >> 16;
      int participation_profit = profit - validation_profit;
      int validator_share = participation_profit * val_balance /(val_balance + nom_balance);
      nominator_profit = participation_profit - validator_share;
      val_balance += validation_profit + validator_share;
      nom_balance += nominator_profit;
    } else {
        ;; first distribute nomination part profit to validator, than to nominator
        if (profit + val_balance >= 0) {
          val_balance += profit;
        } else {
          nominator_profit = val_balance + profit;
          nom_balance += nominator_profit;
          val_balance = 0;
        }
    }
    if ( nominator_profit ) {
      notify_nominator(nominator, nominator_profit, nom_balance, fees::profit_notification());
    }
    (val_request, nom_request)~consider_withdrawal_requests(validator, nominator, val_balance, nom_balance);
    return ((val_balance, nom_balance, val_request, nom_request), ());
}
